buildscript {

    ext {
        tag = "spring" as String
        prefix = "msa" as String
        springCloudVersion = "Hoxton.SR8" as String
    }

    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "com.palantir.gradle.docker:gradle-docker:0.25.0"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:2.3.3.RELEASE"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.10.RELEASE"
    }
}

group 'me.sun'
version '0.0.1-SNAPSHOT'

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.palantir.docker'

    group = 'me.sun'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '1.8'

    ext {
        dockerName = "$prefix/$project.name:$tag"
        scriptPath = "src/main/docker/run.sh"
        jarPath = "$buildDir/libs/"
        dockerFile = "src/main/docker/Dockerfile"
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
    }

    test {
        useJUnitPlatform()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }
}
